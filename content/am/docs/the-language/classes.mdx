---
title: ክፍሎች
description: ክፍሎች ነገር ተኮር ፕሮግራሞችን መስሪያ መንገዶች ናቸው።
---

# ክፍሎችን መሰየም

ክፍሎችን እንደሚከተለው እንሰይማለን

```ahadu
ክፍል የዋጋ_አወጣጥ {
    // class body
}
```

## ክፍል ማስጀመሪያ

የክፍል `ማስጀመሪያ` መንገድ ለየት ያለ ተግባር ሲሆን ነገሮችን እንደማስጀመሪያነት እንጠቀምበታለን። በአንድ ክፍል ውስጥ አንድ `ማስጀመሪያ` ብቻ ነው መሰየም የሚቻለው።

```ahadu
ክፍል የዋጋ_አወጣጥ {
    ማስጀመሪያ(ብር, ምንዛሪ) {} // [!code ++]
    // class body
}
```

## አሁን ላይ ያለውን ነገር መጥቀስ

አሁን ላይ ያለውን ነገር ወይም ክፍል ለመጥቀስ ወይም ለመጥራት `ይህ` የትባለውን ቁልፍ ቃል እንጠቀማለን።

```ahadu
ክፍል የዋጋ_አወጣጥ {
    ማስጀመሪያ(ብር, ምንዛሪ) {
        ይህ.ብር = ብር; // [!code ++]
        ይህ.ምንዛሪ = ምንዛሪ; // [!code ++]
    }
    // class body
}
```
ከላይ አሁን ላይ ያለውን ነገር ብር እና ምንዛሪ እየሰየምኩ ነው።

## የክፍል መንገድን መሰየም

የክፍል መንገድን ለመሰየም `ማስጀመሪያ`ን የሰየምንበትን መንገድ እንጠቀማለን።

```ahadu
ክፍል የዋጋ_አወጣጥ {
    ማስጀመሪያ(ብር, ምንዛሪ) {
        ይህ.ብር = ብር;
        ይህ.ምንዛሪ = ምንዛሪ;
    }
    
    ዋጋውን_አውጣ() { // [!code highlight]
        አውጣ ይህ.ብር + " " + ይህ.ምንዛሪ;
    }
}

መለያ ዋጋ = የዋጋ_አወጣጥ(100, "Dollar");
ዋጋ.ዋጋውን_አውጣ(); // prints 100 Dollar
```

# ክፍሎችን ማዋረስ

አንድ ክፍል አንድ ክፍልን ይወርሳል ለማለት አዲስ በሚፈጠረው ክፍል (በወራሹ ክፍል) እና አዲሱ ክፍል የሚወርሰው ክፍል መሃል `<` ቁለፍ ቃልን እንጠቀማለን።

አንድ ክፍል አንድ ክፍልን ወረሰ ስንል ወራሹ ክፍል የተወራሹን ሙሉ ባህርይ ይወስዳል ማለታችን ነው።

```ahadu
ክፍል ምንዛሪ {
    ማስጀመሪያ(የምንዛሪ_አይነት) {
        ይህ.የምንዛሪ_አይነት = የምንዛሪ_አይነት;

        ከሆነ (የምንዛሪ_አይነት == "Dollar") {
            ይህ.ኮድ = "$"
        } ካልሆነ {
            ይህ.ኮድ = የምንዛሪ_አይነት
        }
    }
}


ክፍል የዋጋ_አወጣጥ < ምንዛሪ { // [!code highlight]
    ማስጀመሪያ(ብር, ምንዛሪ) {
        ታላቅ(ምንዛሪ); // [!code ++]
        ይህ.ብር = ብር;
    }
    ዋጋውን_አውጣ() {
        አውጣ ይህ.ብር + " " + ይህ.የምንዛሪ_አይነት + " (" + ይህ.ኮድ + ")";
    }
}

መለያ ዋጋ = የዋጋ_አወጣጥ(100, "Dollar");
ዋጋ.ዋጋውን_አውጣ(); // prints 100 Dollar ($)
```

`ታላቅ` ምንድነው ብለው የጠይቁ የሆናል `ታላቅ` ተወራሹን ክፍል የምንጠራበት መንገዳችን ነው እናም ከላይ ታላቁን ክፍል `ማስጀመሪያው` ከሚፍለጋቸው መለኪያዎች ጋ እያስጀመርኩት ነው። ታላቅን ሌላ የ ፕሮግራም ቋንቋዎች ላይ `super` በማለት እናውቀዋለን።
