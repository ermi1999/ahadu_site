---
title: Classes
description: Classes are a template for creating objects.
---

# Class definition

defining classes are straight forward, you can define a new class like

```ahadu
ክፍል የዋጋ_አወጣጥ {
    // class body
}
```

## Class initializer
A class `ማስጀመሪያ` method is a special function that's used to initialize objects when they are created. It allows you to set up the initial state or attributes of an object. there can only be one initializer with a name `ማስጀመሪያ` in a class.

```ahadu
ክፍል የዋጋ_አወጣጥ {
    ማስጀመሪያ(ብር, ምንዛሪ) {} // [!code ++]
    // class body
}
```
## Referencing the current object
To reference the current object you can use the `ይህ` keyword.

```ahadu
ክፍል የዋጋ_አወጣጥ {
    ማስጀመሪያ(ብር, ምንዛሪ) {
        ይህ.ብር = ብር; // [!code ++]
        ይህ.ምንዛሪ = ምንዛሪ; // [!code ++]
    }
    // class body
}
```

here i am initializing ብር and ምንዛሪ attributes to the current object.


## defining a class method
defining a class method is straight forward, you just define the method name and an opening bracket followed by optional arguments and a function body.

```ahadu
ክፍል የዋጋ_አወጣጥ {
    ማስጀመሪያ(ብር, ምንዛሪ) {
        ይህ.ብር = ብር;
        ይህ.ምንዛሪ = ምንዛሪ;
    }
    
    ዋጋውን_አውጣ() { // [!code highlight]
        አውጣ ይህ.ብር + " " + ይህ.ምንዛሪ;
    }
}

መለያ ዋጋ = የዋጋ_አወጣጥ(100, "Dollar");
ዋጋ.ዋጋውን_አውጣ(); // prints 100 Dollar
```
# Class Inheritance

To create a class inheritance, use `<` keyword between the class that is being created and the the class that the new class  inherits from.

A class created with a class inheritance inherits all the methods from another class.

```ahadu
ክፍል ምንዛሪ {
    ማስጀመሪያ(የምንዛሪ_አይነት) {
        ይህ.የምንዛሪ_አይነት = የምንዛሪ_አይነት;

        ከሆነ (የምንዛሪ_አይነት == "Dollar") {
            ይህ.ኮድ = "$"
        } ካልሆነ {
            ይህ.ኮድ = የምንዛሪ_አይነት
        }
    }
}


ክፍል የዋጋ_አወጣጥ < ምንዛሪ { // [!code highlight]
    ማስጀመሪያ(ብር, ምንዛሪ) {
        ታላቅ(ምንዛሪ); // [!code ++]
        ይህ.ብር = ብር;
    }
    ዋጋውን_አውጣ() {
        አውጣ ይህ.ብር + " " + ይህ.የምንዛሪ_አይነት + " (" + ይህ.ኮድ + ")";
    }
}

መለያ ዋጋ = የዋጋ_አወጣጥ(100, "Dollar");
ዋጋ.ዋጋውን_አውጣ(); // prints 100 Dollar ($)
```
when i first initialize the class, i call the parent class with `ታላቅ` to initialize it, `ታላቅ` is equivalent to `super` in other languages.